include_directories("${AvogadroLibs_BINARY_DIR}/avogadro/core"
  "${AvogadroLibs_BINARY_DIR}/avogadro/qtgui"
  "${AvogadroLibs_SOURCE_DIR}" "${AvogadroLibs_BINARY_DIR}")

# find google test
find_package(GTest REQUIRED)
find_package(Eigen3 REQUIRED)
# Add both as "system headers" to avoid warnings generated by them with
# compilers that support that notion.
include_directories(SYSTEM
  ${GTEST_INCLUDE_DIRS}
  ${EIGEN3_INCLUDE_DIR})

# Check if we need extra include directories for C++11 features.
if(AvogadroLibs_NEEDS_BOOST)
  include_directories(SYSTEM
    ${AvogadroLibs_MUTEX_INCLUDE_DIRS}
    ${AvogadroLibs_MEMORY_INCLUDE_DIRS})
endif()

include(CheckIncludeFileCXX)
include(CheckCXXSymbolExists)
check_include_file_cxx("gtest/gtest.h" HAVE_GTEST_HPP)
if(HAVE_GTEST_HPP)
  check_cxx_symbol_exists(GTEST_HAS_PTHREAD "gtest/gtest.h" GTEST_HAS_PTHREAD)
  check_cxx_symbol_exists(GTEST_IS_THREADSAFE "gtest/gtest.h" GTEST_IS_THREADSAFE)
endif()

if(GTEST_HAS_PTHREAD)
  message(STATUS "GTest claims it has pthreads, we need to link to it.")
  find_package(Threads)
  set(EXTRA_LINK_LIB ${CMAKE_THREAD_LIBS_INIT})
else()
  set(EXTRA_LINK_LIB "")
endif()

find_path(AVOGADRO_DATA_ROOT .avogadro.data
  ${AvogadroLibs_SOURCE_DIR}/../avogadrodata
  $ENV{AVOGADRO_DATA_ROOT}
  DOC "The repository for data used for testing."
  )
mark_as_advanced(AVOGADRO_DATA_ROOT)

# Specify the name of each test (the Test will be appended where needed).
set(tests
  Array
  Atom
  Bond
  CoordinateSet
  CPP11
  Eigen
  Element
  Graph
  Molecule
  RingPerceiver
  Variant
  VariantMap
  )

# Build up the source file names.
set(testSrcs "")
foreach(TestName ${tests})
  message(STATUS "Adding ${TestName} test.")
  string(TOLOWER ${TestName} testname)
  list(APPEND testSrcs ${testname}test.cpp)
endforeach()
message(STATUS "Test source files: ${testSrcs}")

# Add a single executable for all of our tests.
add_executable(AvogadroTests ${testSrcs})
target_link_libraries(AvogadroTests AvogadroCore
  ${GTEST_BOTH_LIBRARIES} ${EXTRA_LINK_LIB})
# Additional libraries and compiler definitions necessary when using Boost to
# replace C++11.
if(AvogadroLibs_NEEDS_BOOST)
  target_link_libraries(AvogadroTests ${AvogadroLibs_MUTEX_BOOST_LIBRARIES})
  add_definitions(${AvogadroLibs_BOOST_DEFINITIONS})
endif()

# Now add all of the tests, using the gtest_filter argument so that only those
# cases are run in each test invocation.
foreach(TestName ${tests})
  add_test(NAME "${TestName}Test"
    COMMAND AvogadroTests "--gtest_filter=${TestName}Test.*")
endforeach()

# This will get refactored soon - test per module.
add_subdirectory(io)
add_subdirectory(qtopengl)
